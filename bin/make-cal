#!/usr/bin/env raku

use Calendar;

if not @*ARGS {
    print qq:to/HERE/;
    Usage: {$*PROGRAM.basename} <mode> [options...]

    Modes:
        files  - Creates a sample CSV file for personalization
        caldat - Runs the Linux 'cal' program and prints results to stdout
        help   - Extended help, including language ISO codes

    Options:
        lang=X - ISO language code, the default is 'en' (English)
        y=YYYY - The default is the next calendar year. Note years prior to
                 2019 cannot be created correctly due to lack of data.
        m=M    - The month for option 'caldat' only (1..12).
        debug

    HERE
    exit;
}

# modes
my $caldat = 0;
my $files  = 0;
my $help   = 0;

# options
my $debug  = 0;
my $lang   = 'en';
my $month; # undefined at this point
my $year = DateTime.now.year + 1;
for @*ARGS {
    when /:i '-'? c/ { ++$caldat }
    when /:i '-'? d/ { ++$debug  }
    when /:i '-'? f/ { ++$files  }
    when /:i '-'? h/ { ++$help   }
    when /:i '-'? 'lang=' (\w\w) $/ { 
        $lang = ~$0;
    }
    when /:i '-'? 'm=' (\d**1..2) $/ { 
        $month = +$0;
        # note "DEBUG: entered month: $month"; exit;
        if not (0 < $month < 13) {
            note "FATAL: The month must be in the range [1..12].";
            exit;
        }
    }
    when /:i '-'? 'y=' (\d**4) $/ { 
        $year = +$0;
        #note "DEBUG: entered year: $year"; exit;
        if $year < 2019 {
            note "FATAL: Unable to handle years earlier than 2019.";
            exit;
        }
    }
    default {
        die "FATAL: Unknown arg '$_'"
    }
}

# must have a mode selected
if not ($caldat or $files or $help) {
    die "FATAL: You must select one mode.";
}

if $help {
   help;
}

if $caldat {
    my $o = Calendar.new: :$year, :$lang;
    if $month.defined {
        $o.caldata: $month, :$debug;
    }
    else {
        $o.caldata: :$debug;
    }
    exit;
}

if $files {
    note "Showing an annotated events CSV file.";
    show-events-file :$debug;
    exit; 
}


sub help is export {
    print qq:to/HERE/;
    Usage: {$*PROGRAM.basename} <mode> [options...]

    HERE

    print q:to/HERE/;
    Modes:
        files  - Creates a sample CSV file for personalization
        caldat - Runs the Linux 'cal' program and prints results to stdout
        help   - Extended help, including language ISO codes

    Options:
        lang=X - ISO language code, the default is 'en' (English)
        y=YYYY - The default is the next calendar year. Note years prior to
                 2019 cannot be created correctly due to lack of data.
        m=M    - The month for option 'caldat' only (1..12).
        debug

    Following are the ISO codes for languages usable for calendar output:

        Language            | ISO code
        --------------------+---------
        Dutch               | nl
        English             | en
        French              | fr
        German              | de
        Indonesian          | id
        Italian             | it
        Norwegian (Bokm√•l)  | nb
        Norwegian (Nynorsk) | nn
        Polish              | pl
        Romanian            | ro
        Russian             | ru
        Spanish             | es
        Ukranian            | uk

    HERE
    exit;
}

